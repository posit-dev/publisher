# Build and run all tests against in development
default: dev

# License file path for Connect (override with LICENSE=path)
LICENSE := env_var_or_default("CONNECT_LICENSE_FILE", env_var("HOME") + "/connect-license.lic")

# Install Cypress dependencies using npm
install:
    npm install

# Build a docker image using docker compose
build-image service:
    docker compose build "{{service}}"

# Build the base image (only needed on first setup)
build-base:
    docker build -f Dockerfile.base --tag e2ebase --platform linux/amd64 .

start service:
    docker compose up -d "{{service}}"

# Stop all services using docker compose
stop:
    docker compose down --volumes --remove-orphans

lint:
    npm run lint

clear-credentials:
    #!/usr/bin/env bash
    set -euo pipefail

    cat <<EOF > e2e-test.connect-credentials
    # File updated and managed by e2e tests. Refrain from updating it manually.

    EOF

# Build publisher and run e2e tests (requires with-connect CLI and license file)
dev:
    #!/usr/bin/env bash
    set -euo pipefail

    just build-publisher
    just e2e

# Run e2e tests with Connect managed by with-connect
# Requires: with-connect CLI (uv tool install git+https://github.com/posit-dev/with-connect.git)
# Set LICENSE env var or CONNECT_LICENSE_FILE to specify license path
e2e *cypress_args:
    #!/usr/bin/env bash
    set -euo pipefail

    # Check for with-connect
    command -v with-connect >/dev/null 2>&1 || {
        echo "Error: with-connect CLI not found. Install with: uv tool install git+https://github.com/posit-dev/with-connect.git"
        exit 1
    }

    # Check for license file
    if [[ ! -f "{{LICENSE}}" ]]; then
        echo "Error: License file not found at {{LICENSE}}"
        echo "Set CONNECT_LICENSE_FILE env var or LICENSE=path when running just"
        exit 1
    fi

    function cleanup() {
        just clear-credentials
    }
    trap cleanup EXIT

    just clear-credentials
    just install
    just start "code-server"

    # Install the Publisher extension
    VSIX_FILENAME=$(ls -Art ../../dist | grep linux-amd64 | tail -n 1)
    docker compose exec code-server code-server --install-extension "/home/coder/vsix/${VSIX_FILENAME}"

    # Run tests with Connect managed by with-connect
    # On macOS, DOCKER_HOST may need to be set: export DOCKER_HOST=unix://$HOME/.docker/run/docker.sock
    with-connect --license "{{LICENSE}}" -- \
        bash -c 'CYPRESS_BOOTSTRAP_ADMIN_API_KEY=$CONNECT_API_KEY npx cypress run {{cypress_args}}'

# Run Cypress in interactive mode with Connect
e2e-open:
    #!/usr/bin/env bash
    set -euo pipefail

    command -v with-connect >/dev/null 2>&1 || {
        echo "Error: with-connect CLI not found. Install with: uv tool install git+https://github.com/posit-dev/with-connect.git"
        exit 1
    }

    if [[ ! -f "{{LICENSE}}" ]]; then
        echo "Error: License file not found at {{LICENSE}}"
        exit 1
    fi

    just start "code-server"

    # Install the Publisher extension
    VSIX_FILENAME=$(ls -Art ../../dist | grep linux-amd64 | tail -n 1)
    docker compose exec code-server code-server --install-extension "/home/coder/vsix/${VSIX_FILENAME}"

    with-connect --license "{{LICENSE}}" -- \
        bash -c 'CYPRESS_BOOTSTRAP_ADMIN_API_KEY=$CONNECT_API_KEY npx cypress open'

# Build all required Docker images
# Note: build-base only needed on first setup or when Dockerfile.base changes
build-images:
    docker build -f Dockerfile.base --tag e2ebase --platform linux/amd64 .
    docker compose build code-server
    just pull-workbench "release"

build-publisher:
    USE_PLATFORM="linux/amd64" just ../../



# =====================================================================
# Workbench-specific commands
# These commands use the Workbench services defined in docker-compose.yml
# The Workbench images include the default user: rstudio/rstudio 
# as documented here: https://hub.docker.com/r/rstudio/rstudio-workbench
# =====================================================================

# Pull the Workbench container image
pull-workbench service="release":
    docker compose pull workbench-{{ service }}

# Start a Workbench container
start-workbench service="release":
    ../scripts/start-workbench.sh "{{ service }}"

# Stop a Workbench container
stop-workbench service="release":
    docker compose stop workbench-{{ service }}

# Remove Workbench containers
remove-workbench service="release":
    #!/usr/bin/env bash
    set -euo pipefail

    # First stop the container
    just stop-workbench "{{ service }}"

    # Remove the container
    docker compose rm -f workbench-{{ service }}

# Install Publisher extension in a running Workbench container
install-positron-extension service="release":
    ../scripts/install-positron-extension.sh "{{ service }}"

# Check if publisher extension is installed in Positron
# This command is designed to work from any directory (Cypress or command line)
check-positron-extension:
    ../scripts/check-positron-extension.sh

# Extract Positron logs from Workbench container
extract-positron-logs service="release":
    ../scripts/extract-positron-logs.sh "{{ service }}"
