_use_docker := env_var_or_default("DOCKER", "true")

# will run recipes: bootstrap, validate, build, and test
default: bootstrap validate build test

# update javascript/typescript dependencies
bootstrap:
    #!/bin/bash
    set -euo pipefail

    # set this environment variable to keep Cypress from attempting
    # to create a cache within root's home dir.
    export CYPRESS_CACHE_FOLDER="./test/cypress/.cache"
    echo "NOTE: npm install may take five minutes or more from within a docker container..."
    npm install --no-audit

# validate the source files, do not fix the fixable items
validate:
    npx eslint --ext .js,.ts,.vue,.cjs ./

# validate the source files, fix the fixable items
validate-fix:
    npx eslint --ext .js,.ts,.vue,.cjs ./ "--fix"

# build the web artifacts for the SPA (into dest/spa)
build:
    #!/bin/bash
    set -euo pipefail

    just bootstrap

    if ! npx quasar build; then
     echo ""
     echo "The build has failed."
     echo "If you encountered the error: 'Global Quasar CLI • ⚠️   Error  Unknown command \"build\"'"
     echo "Try running 'just bootstrap' and then retrying your build command."
     exit 1
    fi

# perform unit, go race and e2e tests
test: test-unit test-race test-e2e

# perform race testing
test-race:
    go test -race ./...

# run e2e (Cypress) tests
test-e2e:
    #!/bin/bash
    set -euo pipefail

    # set this environment variable to keep Cypress from attempting to create
    # cache within root's home dir.
    export CYPRESS_CACHE_FOLDER="./test/cypress/.cache"
    # install cypress
    npx cypress install

    npx quasar test --e2e cypress

test-ci-e2e target:
    #!/bin/bash
    set -euo pipefail
    just build
    # set this environment variable to keep Cypress from attempting to create
    # cache within root's home dir.
    # use a different cache per os
    export CYPRESS_CACHE_FOLDER="./test/cypress/{{target}}/.cache"
    # install cypress
    npx cypress install
    npx quasar test --e2e cypress

# run unit tests one time
test-unit:
    # npx vitest run
    npx quasar test --unit vitest

# run unit tests in watch mode, re-running as files are changed
test-unit-watch:
    npx vitest

# start common UX development flow. Start the web server which updates automatically upon file changes
dev:
    npx quasar dev

# remove build artifacts and dependencies
clean:
    #!/usr/bin/env bash
    set -euo pipefail

    # not using quasar clean, as it would require us to have node_modules fully installed. Instead, performing what
    # it said it was doing...
    # quasar clean
    # App • Cleaned build artifact: "/work/web/.quasar"
    # App • Emptied dist folder
    # App • Done cleaning build artifacts
    rm -rdf .quasar
    rm -rdf dist/spa
    rm -rdf node_modules
