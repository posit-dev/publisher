# Executes clean, install, test, build
default: install lint test build

export CYPRESS_CACHE_FOLDER := "./cypress/.cache"

_ci := "${CI:-false}"

_with_debug := if env_var_or_default("DEBUG", "true") == "true" {
        "set -x pipefail"
    } else {
        ""
    }

# Compiles the static assests for the web client. Static assets are written to `./web/dist`.
build:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run build

# Deletes ephermal project files (i.e., cleans the project).
clean:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    rm -rf .quasar
    rm -rf dist
    rm -rf node_modules

# Executes end-to-end tests.
e2e:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    # npx cypress install
    npm run e2e

# Launch a local web server instance with hot-reloads.
dev:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run dev

# Fixes formatting using ESLint
fix:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run fix

# Installs dependencies
install:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    if [ {{ _ci }} = "true" ]; then
        npm ci --no-audit --no-fund
    else
        npm install --no-audit --no-fund
    fi

# Checks formatting using ESLint
lint:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run lint

# Launch a local web server instance in production mode.
preview:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run preview

# Executes commands via `npm`. Equivalent to `npm run`. Provides arbitrary command chaining inside of Docker from project root (i.e., `just web run preview`)
run *args:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run {{ args }}


# Executes unit tests
test:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run test

# Watches unit tests
watch:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run watch
